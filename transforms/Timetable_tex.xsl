<?xml version="1.0" ?>
<xsl:stylesheet version="1.0"
		xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

<xsl:output method="text" />

<xsl:template match="period">  \makewhite{} \periodlabel{<xsl:value-of select="concat(./@number, ' ', ./@starttime)" />}<xsl:apply-templates select="year" /> \hline
</xsl:template>

<xsl:template match="year">
    <xsl:if test="not(./@number='1')">\makewhite{}</xsl:if> <xsl:if test="./@number='1' or ./@number='3'">\shade{} </xsl:if> &amp; <xsl:value-of select="./@number" />\yearstrut{} &amp; <xsl:apply-templates select="day"/> \\
</xsl:template>

<xsl:template match="day">
  <xsl:choose> <xsl:when test="./module/@language='B'"> \widebox{<xsl:if test="../@number='1' or ../@number='3'">>{\shade{}}</xsl:if>c|}{<xsl:apply-templates select="module[./@language='B']"/>} </xsl:when> 
  <xsl:otherwise> 
    \narrowbox{<xsl:apply-templates select="module[./@language='A']" />} &amp; \narrowbox{<xsl:apply-templates select="module[./@language='E']" />} </xsl:otherwise> </xsl:choose> <xsl:if test="not(position()=last())">&amp;</xsl:if>
</xsl:template>

<xsl:template match="module"> \subject{<xsl:if test="./@type='P' or ./@type='T' or ./@type='O'">(</xsl:if> <xsl:value-of select="./@name" /> <xsl:if test="./@type='P' or ./@type='T' or ./@type='O'">)</xsl:if>} \\ \venue{<xsl:value-of select="./@venue"/>} <xsl:if test="not(position()=last())">\\</xsl:if> </xsl:template>

<xsl:template match="semester">
  <xsl:param name="startperiod" select="1" />
  <xsl:param name="endperiod" select="10" />
  \printtitle{<xsl:value-of select="/timetable/geninfo/@dept"/> - Semester <xsl:value-of select="./@number"/> of <xsl:value-of select="/timetable/@year"/> (generated on <xsl:value-of select="/timetable/geninfo/@date"/> )} \\ 

  \fontsize{7pt}{2pt}\usefont{T1}{phv}{m}{n}
  \begin{tabular}{|ll|c|c|c|c|c|c|c|c|c|c|c}
    \hline
    &amp; Yr &amp; \multicolumn{2}{c|}{Ma/Mo} &amp;
    \multicolumn{2}{c|}{Di/Tu} &amp;
    \multicolumn{2}{c|}{Wo/We} &amp;
    \multicolumn{2}{c|}{Do/Th} &amp;
    \multicolumn{2}{c|}{Vr/Fr} \\
    &amp; &amp; Afr &amp; Eng &amp; Afr &amp; Eng &amp; Afr &amp; Eng &amp; Afr &amp; Eng &amp; Afr &amp; Eng \\
    \hline
    <xsl:apply-templates select="period[(./@number >= $startperiod) and ($endperiod >= ./@number) ]" />
    \end{tabular}
    \credits{Generated by <xsl:value-of select="/timetable/geninfo/@generator" /> version <xsl:value-of select="/timetable/geninfo/@version" />}
  \newpage

</xsl:template>

<xsl:template match="/timetable">
  \documentclass[a4paper]{article}
  \usepackage[margin=3mm]{geometry}
  \usepackage{multirow}
  \usepackage{color}
  \definecolor{countershade}{gray}{0.95}
  \usepackage{colortbl}
  \usepackage{graphicx}
  \newcommand\widebox[2]{\multicolumn{2}{#1}{\parbox{0.14\textwidth}{\centering #2}}}
  \newcommand\narrowbox[1]{\parbox{0.07\textwidth}{\centering #1}}
  \newcommand\subject[1]{{\fontsize{8pt}{2pt}\usefont{T1}{phv}{m}{n} #1}}
  \newcommand\venue[1]{{\fontsize{6pt}{2pt}\usefont{T1}{phv}{m}{n} #1}}
  \newcommand\printtitle[1]{\fontsize{10pt}{2pt}\usefont{T1}{phv}{m}{n} #1}
  \newcommand\credits[1]{\fontsize{5pt}{2pt}\usefont{T1}{phv}{m}{n} #1}
  \newcommand\yearstrut{\rule{0pt}{2ex}}
  \newcommand\shade{\rowcolor{countershade}}
  \newcommand\periodlabel[1]{\multirow{4}{*}{\rotatebox{90}{#1}}}
  \newcommand\makewhite{\multicolumn{1}{|c}{}}
  \pagestyle{empty}
  \thispagestyle{empty}
  \begin{document}
  \tiny
  \centering
  <xsl:for-each select="semesters/semester">
    <xsl:apply-templates select="." >
      <xsl:with-param name="startperiod" select="1" />
      <xsl:with-param name="endperiod" select="11" />
    </xsl:apply-templates>
  </xsl:for-each>
  \end{document}
</xsl:template>

</xsl:stylesheet>
